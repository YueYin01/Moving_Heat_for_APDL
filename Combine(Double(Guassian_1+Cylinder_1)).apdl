!   Commands inserted into this file will be executed just prior to the ANSYS SOLVE command.
!   These commands may supersede command settings set by Workbench.

!   Active UNIT system in Workbench when this object was created:  Metric (m, kg, N, s, V, A)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.

! 来自https://www.bilibili.com/video/BV1kh411q7zx
! 热对流要1500s之后才能观察到效果，所以要算很久

CSYS,0              ! 设定坐标系为全局坐标系（ID为0）
CMSEL,ALL           ! 选取所有单元

! 获取模型中的单元编号的最大和最小值
*GET,EMAX,ELEM,,NUM,MAX
*GET,EMIN,ELEM,,NUM,MIN
ALLSEL              ! 取消所有的选择

! 定义双高斯圆柱热源的公式中的参数值
Q1=18000             ! 初始热源强度，热源量级和数值大小需要仔细计算，这个峰值温度是905℃左右
Q2=14000 
r1=0.010              ! 热源半径
r2=0.005
Y1=-0.005          ! Y 方向的热源深度(圆柱高度)；就是视频中的c值
Y2=-0.014
dx=0.0                ! 这个是什么值？
dz=0.0
PI=3.14159          ! π值
VEL=0.037           ! 热源移动的速度

time_weld=21       ! 焊接总时间+1s冷却缓冲；会覆盖设置中的时间，需要通过另一个代码Cooling补充冷却时间
time_inc=0.1        ! 时间步长
time_steps=time_weld/time_inc       ! 时间步数

NROPT,FULL            ! 使用完全积分的Newton-Raphson方程求解

! 在每个时间步中循环，模拟焊接过程
*DO,i,1,time_steps,1
WTIME=(i*time_inc)
TIME,WTIME
HCENTER=VEL*WTIME        ! 热源中心，后边似乎没用到？

! 全局笛卡尔坐标系中第j个元素的质心xyz坐标，质心是从元素上选定的节点确定的。
*DO,jj,EMIN,EMAX,1
X=CENTRX(jj)
Y=CENTRY(jj)
Z=CENTRZ(jj) 
CSI_X=X-dx
CSI_Z=Z-(VEL*WTIME)-dz     ! 这里是减么？还是加？
r0=sqrt((CSI_X)**2+(CSI_Z)**2)

! 循环热源分布载荷，如果X、Y方向在半径和深度范围内，则计算热源强度；否则为0
*IF,Y,LT,Y2,THEN
Q=0
*ELSEIF,r0,GT,r1,THEN
Q=0
*ELSEIF,Y,GE,Y1,AND,r0,LE,r1,THEN          ! 需要确认一下这个公式有没有问题？公式中的u(z)也没体现？
Q=Q1/(PI*(r1**2)*ABS(Y1))*exp((-1)*(CSI_X**2+CSI_Z**2)/r1**2)
! Q=1.2e10
*ELSEIF,Y,LT,Y1,AND,r0,LE,r2,THEN
Q=Q2/(PI*(r2**2)*(ABS(Y2)-ABS(Y1)))*exp((-1)*(CSI_X**2+CSI_Z**2)/r2**2)
! Q=9e9
*ELSE            
Q=0
*ENDIF

BFE,jj,HGEN,,Q       ! 在单元上应用双高斯圆柱热源，这里需要对Q1和Q2进行仔细计算，否则会发现上下热源温度不同
*ENDDO
SOLVE                ! 解算热传导方程
*ENDDO

! 取消所有的选择
CMSEL,ALL

! 删除所有单元
BFEDELE,ALL,ALL
ALLSEL
