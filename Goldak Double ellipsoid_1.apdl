!   Commands inserted into this file will be executed just prior to the ANSYS SOLVE command.
!   These commands may supersede command settings set by Workbench.

!   Active UNIT system in Workbench when this object was created:  Metric (m, kg, N, s, V, A)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.


! 来自https://www.bilibili.com/video/BV1YP4y147UV ，https://www.bilibili.com/video/BV1PP4y1a7J4
! 热对流要1500s之后才能观察到效果(即最高温度向板中心移动)，所以要算很久

CSYS,0                  ! 设定坐标系为全局坐标系（ID为0）
CMSEL,ALL               ! 选取所有单元

! 获取模型中单元的最大和最小编号。
*GET,EMAX,ELEM,,NUM,MAX
*GET,EMIN,ELEM,,NUM,MIN
ALLSEL                  ! 重新选择所有内容，确保模型中的所有实体都被选择。

! 定义双椭球热源的公式中的参数值
Q1=5000                  ! 初始热源强度，热源量级和数值大小需要仔细计算；目前峰值温度1190℃
A=0.003
B=0.008
C1=0.004
C2=0.016
FF=0.6
FR=1.4                   ! 需要满足FF+FR=2
PI=3.14159
VEL=0.037                ! 热源移动的速度

time_weld=21             ! 焊接总时间+1s冷却缓冲；会覆盖设置中的时间，需要通过另一个代码Cooling补充冷却时间
time_inc=0.1             ! 时间步长
time_steps=time_weld/time_inc       ! 时间步数

NROPT, FULL               ! 完全积分的Newton-Raphson方程

! 在每个时间步中循环，模拟焊接过程
*DO,i,1,time_steps,1
WTIME=(i*time_inc)        ! 计算当前时间
TIME,WTIME                ! 设置当前时间
HCENTER=VEL*WTIME         ! 计算热源的中心位置。后续没用到？

! 在内部循环中，遍历所有的单元
*DO,jj,EMIN,EMAX,1
! 获取全局笛卡尔坐标系中第j个元素的质心xyz坐标，质心是从元素上选定的节点确定的。
X=CENTRX(jj)
Y=CENTRY(jj)
Z=CENTRZ(jj)
CSI=Z-VEL*WTIME           ! 注意坐标系的定义；这里是减么？还是加？

! 循环热源分布载荷，根据公式的定义，前后半球有着不同的参数值
*IF,Z,LT,HCENTER,THEN
C=C1
F=FF
*ELSE
C=C2
F=FR
*ENDIF

PART1=(6*(3**0.5)*F*Q1)/(A*B*C*PI*(PI**0.5))
PART2=(exp((-3)*((X/A)**2+(Y/B)**2+(CSI/C)**2)))
! PART2=(exp(-3*(X/A)**2))*(exp(-3*(Y/B)**2))*(exp(-3*(CSI/C)**2))
Q=PART1*PART2           ! 计算最终的热源强度
! 速度太快可能会导致温度还没降下去，进而导致形状不太好，最好加密网格来进行处理。

BFE,jj,HGEN,,Q
*ENDDO                  ! 结束内部循环，即对所有单元的遍历
SOLVE                   ! 在每个时间步上求解热传导方程
*ENDDO                  ! 结束外部循环，即对时间步的遍历

CMSEL,ALL               ! 选择所有的单元
BFEDELE,ALL,ALL         ! 删除所有单元上的热源
ALLSEL                  ! 重新选择所有内容。

