!   Commands inserted into this file will be executed just prior to the ANSYS SOLVE command.
!   These commands may supersede command settings set by Workbench.

!   Active UNIT system in Workbench when this object was created:  Metric (m, kg, N, s, V, A)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.

! 来自https://www.bilibili.com/video/BV1F3411z7WF， https://www.bilibili.com/video/BV1t44y1b77X
! 热对流要1500s之后才能观察到效果(即最高温度向板中心移动)，所以要算很久

CSYS,0                  ! 设定坐标系为全局坐标系（ID为0）
CMSEL,ALL            ! 选取所有单元

! 获取模型中的单元编号的最大和最小值
*GET,EMAX,ELEM,,NUM,MAX
*GET,EMIN,ELEM,,NUM,MIN
ALLSEL                ! 取消所有的选择

! 定义高斯圆锥热源的公式中的参数值，这个的圆锥其实是圆台
Q1=2.5e10               ! 初始热源强度，热源量级和数值大小需要仔细计算；目前峰值温度为1010℃
r1=0.005              ! 热源尺寸
r2=0.002
PI=3.14159
Y1=0                  ! Y 方向上的阈值，用于判断是否计算热源；
Yi=-0.014             ! 这个应该可以直接定义为板厚0.014，这样可以更明显的看到圆柱
VEL=0.037             ! 热源移动的速度

time_weld=21          ! 焊接总时间+1s冷却缓冲；会覆盖设置中的时间，需要通过另一个代码Cooling补充冷却时间
time_inc=0.1          ! 时间步长
time_steps=time_weld/time_inc            ! 时间步数

NROPT,FULL            ! 使用完全积分的Newton-Raphson方程求解

! 在每个时间步中循环，模拟焊接过程
*DO,i,1,time_steps,1
WTIME=(i*time_inc)
TIME,WTIME
HCENTER=VEL*WTIME        ! 热源中心

! 全局笛卡尔坐标系中第j个元素的质心xyz坐标，质心是从元素上选定的节点确定的。
*DO,jj,EMIN,EMAX,1
X=CENTRX(jj)
Y=CENTRY(jj)
Z=CENTRZ(jj)
CSI=Z-VEL*WTIME                                 ! 这里是减么？还是加？
r0=r1+((r2-r1)/(Yi-Y1))*(Y-Y1)                  ! 公式好像有问题，这里应该是(r2+r1)？

! 循环高斯圆锥热源分布载荷，如果 Y 大于阈值 Yi，则计算热源强度，否则为0
*IF,Y,GE,Yi,THEN
Q=Q1*exp((-1)*(X**2+CSI**2)/(r0)**2)
*ELSE
Q=0
*ENDIF

BFE,jj,HGEN,,Q          ! 在单元上应用高斯圆锥热源
*ENDDO
SOLVE                      ! 解算热传导方程
*ENDDO

! 取消所有的选择
CMSEL,ALL

! 删除所有单元
BFEDELE,ALL,ALL
ALLSEL
