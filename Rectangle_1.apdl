!   Commands inserted into this file will be executed just prior to the ANSYS SOLVE command.
!   These commands may supersede command settings set by Workbench.

!   Active UNIT system in Workbench when this object was created:  Metric (m, kg, N, s, V, A)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.

! 来自https://www.bilibili.com/video/BV1Rz4y1m7w4 ， https://www.bilibili.com/video/BV1PP4y1a7J4
! 热对流要1500s之后才能观察到效果(即最高温度向板中心移动)，所以要算很久

CSYS,0                ! 设定坐标系为全局坐标系（ID为0）
CMSEL,ALL           ! 选择所有的单元

! 获取模型中单元的最大和最小编号。
*GET,EMAX,ELEM,,NUM,MAX
*GET,EMIN,ELEM,,NUM,MIN
ALLSEL              ! 重新选择所有内容，确保模型中的所有实体都被选择。

! 定义圆柱热源的公式中的参数值
Q1=8000             ! 初始热源强度，热源量级和数值大小需要仔细计算；目前峰值温度为1050℃
a1=0.006            ! 热源尺寸
b1=0.004
c1=-0.012           ! 就是其他代码里的Yi，厚度方向的取值
VEL=0.037           ! 热源移动的速度

time_weld=21       ! 焊接总时间+1s冷却缓冲；会覆盖设置中的时间，需要通过另一个代码Cooling补充冷却时间
time_inc=0.1        ! 时间步长，步长需要加一个判断
time_steps=time_weld/time_inc            ! 总的时间步数

NROPT, FULL          ! 使用完全积分的Newton-Raphson方程求解

! 在每个时间步中循环，模拟焊接过程
*DO,i,1,time_steps,1
WTIME=(i*time_inc)            ! 计算当前时间
TIME,WTIME                      ! 设置当前时间
HCENTER=VEL*WTIME       ! 计算热源的中心位置，后边似乎没用到？

! 在内部循环中，遍历所有的单元
*DO,jj,EMIN,EMAX,1
! 获取全局笛卡尔坐标系中第j个元素的质心xyz坐标，质心是从元素上选定的节点确定的。
X=CENTRX(jj)
Y=CENTRY(jj)
Z=CENTRZ(jj)
CSI=Z-VEL*WTIME            ! 注意坐标系的定义，这里是减么？还是加？这个后边没用到啊？

! 循环圆柱热源分布载荷，如果X、Y方向在宽度和深度范围内，则计算热源强度；否则为0
*IF,ABS(X),GT,a1/2,THEN        
Q=0
*ELSEIF,ABS(CSI),GT,b1/2,THEN    
Q=0
*ELSEIF,Y,LT,c1,THEN    
Q=0
*ELSE
Q=Q1/(a1*b1*ABS(c1))       ! 怎么形状有些奇怪啊？
*ENDIF

BFE,jj,HGEN,,Q             ! 在单元上应用圆柱热源
*ENDDO                     ! 结束内部循环，即对所有单元的遍历
SOLVE                      ! 在每个时间步上求解热传导方程
*ENDDO                     ! 结束外部循环，即对时间步的遍历。

CMSEL,ALL                  ! 选择所有的单元
BFEDELE,ALL,ALL            ! 删除所有单元上的热源
ALLSEL                     ! 重新选择所有内容。
